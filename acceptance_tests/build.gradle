apply plugin: 'java'

configurations {
  cucumberRuntime {
    extendsFrom testRuntime
  }
}

idea {
  module {
    testSourceDirs += file('src/acceptanceTest/java')
  }
}

sourceSets {
  acceptanceTest {
    java {
      compileClasspath += test.compileClasspath
      runtimeClasspath += test.runtimeClasspath
      srcDir file('src/acceptanceTest/java')
    }
    resources.srcDir file('src/acceptanceTest/resources')
  }
}

task cucumber(type: Test, dependsOn: 'test', group: 'verification') {
  dependsOn assemble, compileAcceptanceTestJava

  testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
  classpath = sourceSets.acceptanceTest.runtimeClasspath

  testLogging {
    events "started", "passed", "skipped", "failed", "standard_out"
    showExceptions true
    showStackTraces true
    exceptionFormat 'full'
  }
}

dependencies {
  testCompile 'io.cucumber:cucumber-core:2.4.0'
  testCompile 'io.cucumber:cucumber-java:2.4.0'
  testCompile 'io.cucumber:cucumber-junit:2.4.0'
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.9.1'
  testCompile 'org.seleniumhq.selenium:selenium-java:3.11.0'
  testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.11.0'
}
