plugins {
  id "com.moowork.node" version "1.2.0"
}

apply plugin: 'base'

buildDir = 'dist'

node {
  version = '8.1.4'
  download = true
  nodeModulesDir = file("${project.projectDir}")
}

task bundle(type: YarnTask) {
  inputs.dir(new File('src'))
  outputs.dir(new File('dist'))
  args  = ['dist']
}

task start(type: YarnTask) {
  args = ['start']
}

task test(type: YarnTask) {
  args = ['test']
}

task cleanMockup(type: Delete) {
  delete 'mockup'
}

task assembleMockup(type: Tar) {
  archiveName = 'elvaco-mvp-mockup-' + getVersion() +'.tar.gz'
  destinationDir = file('mockup/')

  into('/') {
    from('dist')
    include('**/*')
  }

  into('/') {
    from('.')
    include('mockdata.js')
    include('routes.json')
  }

  into('/') {
    from('../deploy')
    include('elvaco-mvp-mockup.service')
  }

  compression = Compression.GZIP
  extension 'tar'
}

clean.dependsOn(cleanMockup)
check.dependsOn(test)
bundle.dependsOn(yarn_install)
assembleMockup.dependsOn(cleanMockup)
assembleMockup.dependsOn(bundle)
assemble.dependsOn(bundle)
assemble.dependsOn(assembleMockup)
