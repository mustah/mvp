apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'

dependencyManagement {
  imports {mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"}
}

mainClassName = "com.elvaco.geoservice.GeoServiceApplication"
jar {
  baseName = 'geoservice'
  version = '0.1.0'
}

distZip.enabled = false

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.liquibase:liquibase-core'
  compile 'org.hibernate:hibernate-java8'
  compile 'org.hibernate.validator:hibernate-validator:6.0.10.Final'
  compile 'org.hibernate.validator:hibernate-validator-annotation-processor:6.0.10.Final'
  compile 'javax.validation:validation-api:2.0.1.Final'
  compile 'com.h2database:h2'
  compile "org.postgresql:postgresql:${postgresqlVersion}"
  compile 'commons-io:commons-io:2.6'
  compile 'org.springframework.boot:spring-boot-starter-actuator'
  /* Deprecated & removed (http://openjdk.java.net/jeps/320) - used by hibernate */
  compile 'org.glassfish.jaxb:jaxb-runtime:2.3.0'

  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  testCompile "junit:junit:${junitVersion}"
  testCompile "org.assertj:assertj-core:${assertjVersion}"
  testCompile 'org.springframework.boot:spring-boot-starter-test'
}
idea {
  module {
    testSourceDirs += file('src/integrationTest/java')
  }
}
sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}
task integrationTest(type: Test, dependsOn: 'test', group: 'verification') {
  testClassesDirs = sourceSets.integrationTest.output.getClassesDirs()
  classpath = sourceSets.integrationTest.runtimeClasspath
}
configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

check.dependsOn([integrationTest])
